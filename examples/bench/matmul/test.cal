const matmul = fn (n : int, a b : list:<list:<float>>) -> list:<list:<float>> => {
	let mut c = list:<list:<float>>[];
	
	for i in 0..n => {
		let mut ci = list:<float>[0f for _ in 0..n];
		for k in 0..n => {
			let aik : &float = &a[i][k];
			let bk : &list:<float> = &b[k];
			
			for j in 0..n => ci[j] += *aik * bk[j];
		};
		c <<= ci;
	};
	c;
};

const main = fn () => {
	let n : int = 40;
	let tmp : float = 1f / n / n;
	let a = list:<list:<float>>[list:<float>[tmp * (i - j) * (i + j) for j in 0..n] for i in 0..n];
	let b = list:<list:<float>>[list:<float>[tmp * (i - j) * (i + j) for j in 0..n] for i in 0..n];
	
	let d = matmul(n, a, b);
	print(d[n / 2][n / 2]);
	print("Success");
};