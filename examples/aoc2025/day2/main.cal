import File from std::file;
import Channel from std::async;

const main = fn => {
	let file : File = try File.open_read(__file__.replace("main.cal", "input.txt")) : e => {
		print(e);
		return;
	};
	
	let text = try file.read_all(100000) => panic("Failed to read file.");
	let ranges : list:<str> = text.split(",");
	let worker_count = 4;
	let mut out : Channel:<int> = Channel:<int>.new();
	let wg = spawn for i in 0..worker_count => worker_sum(ranges, i, worker_count, out);
	
	wg.wait();
	let mut sum = 0;
	
	for _ in 0..worker_count => {
		let part = match out.get() {
			.Some : v => v,
			.None => 0
		};
		sum += part;
	};
	
	print(sum);
};

const worker_sum = fn (ranges : list:<str>, start_idx : int, step : int, out : Channel:<int>) => {
	let mut idx = start_idx;
	let mut local = 0;
	
	for idx < len(ranges) => {
		local += handle_range(ranges[idx]);
		idx += step;
	};
	
	let mut ch = out;
	ch.send(local);
};

const handle_range = fn (cur : str) -> int => {
	let cur : list:<str> = cur.split("-");
	let start = cur[0] as! int;
	let end = cur[1] as! int;
	if end < start => return 0;
	let mut sum = 0;
	let mut k = 1;
	
	for k <= 10 => {
		let p = 10.pow(k);
		let mul = p + 1;
		let min_x = if k == 1 => 1 else => (p / 10);
		let max_x = p - 1;
		let low = start.ceil_div(mul);
		let high = end.floor_div(mul);
		let x_low = if low < min_x => min_x else => low;
		let x_high = if high > max_x => max_x else => high;
		
		const sum_interval = fn (low : int, high : int) -> int => {
			if high < low => return 0;
			((low + high) * (high - low + 1)) / 2;
		};
		
		if x_low <= x_high => sum += mul * sum_interval(x_low, x_high);
		k += 1;
	};
	
	return sum;
};